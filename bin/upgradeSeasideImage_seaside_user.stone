#!/usr/bin/env superdoit_stone
options
{
	SuperDoitOptionalOptionWithNoArg long: 'step2'.
	SuperDoitOptionalOptionWithNoArg long: 'step4'.
}
%
usage
-----
USAGE 
  $basename [--help | -h] [--debug | -D]

DESCRIPTION
  2. As <seasideUserName>, GsuAbstractGsDevKitUpgrade>>prepareImage_pragma_user

OPTIONS
  -h, --help                 display usage message
  -D, --debug                bring up topaz debugger in the event of a script error

EXAMPLES
  $basename --help
  $basename -D
  $basename
-----
%
method
bannerLog: message
	self stdout
		nextPutAll: '======';
		lf;
		nextPutAll: message;
		lf;
		nextPutAll: '======';
		lf
%
method
logAndWaitForDebug: ex
	| listenForDebug loop |
	listenForDebug := (System gemConfigurationAt: 'GEM_LISTEN_FOR_DEBUG') == true.
	listenForDebug
		ifTrue: [ 
			ex messageText.	"prime the details ... don't want exception class, etc."
			self stdout
				nextPutAll: '---------------------';
				lf;
				nextPutAll: ex details;
				lf;
				nextPutAll: '---------------------';
				lf;
				nextPutAll: (GsProcess stackReportToLevel: 300);
				lf;
				nextPutAll: '---------------------';
				lf;
				nextPutAll: 'GsProcess @' , GsProcess _current asOop printString;
				yourself.
			listenForDebug
				ifTrue: [ 
					self stdout
						nextPutAll:
							' (DEBUGGEM ' , (System gemVersionReport at: 'processId') asString , ')' ].
			self stdout
				lf;
				nextPutAll: '---------------------';
				lf ].
	loop := true.
	[
		[ loop ] whileTrue: [
			"loop waiting for DEBUGGEM to attach"
			(Delay forSeconds: 1) wait ]
	] on: Break do: [:br | 
		"stop looping if DEBUGGEM attach has occured (soft break)"
		loop := false. br pass ]
%
method
doStep2
	self bannerLog: '2. As ', System myUserProfile userId, ', GsuAbstractGsDevKitUpgrade>>prepareImage_pragma_user'.
	(((AllUsers userWithId: 'SystemUser') 
		objectNamed: #UserGlobals) 
			at: #'GsDevKit_Image_Upgrade')
				prepareImage_pragma_user.
	System commit.
	self bannerLog: 'Finished Step 2. ----- ', self basename.
	^ self noResult
%
method
doStep4
	self bannerLog: '4. As ', System myUserProfile userId, ', GsuAbstractGsDevKitUpgrade>>prepareImage_user and GsuAbstractGsDevKitUpgrade>>prepareGsDevKitImage'.
	(((AllUsers userWithId: 'SystemUser') 
		objectNamed: #UserGlobals) 
			at: #'GsDevKit_Image_Upgrade')
				prepareImage_user;
				prepareGsDevKitImage.
	System commit.
	self bannerLog: 'Finished Step 4. ----- ', self basename.
	^ self noResult
%
doit
	self bannerLog: 'Running ----- ', self basename.
	[
		self step2 ifTrue: [ ^ self doStep2 ].
		self step4 ifTrue: [ ^ self doStep4 ].
		self error: 'Expected option --step2 or --step4'
	] on: Error, Halt
		do: [:ex | 
			(System gemConfigurationAt: 'GEM_LISTEN_FOR_DEBUG') == false
				ifTrue: [ ex pass ].
			self logAndWaitForDebug: ex ].
	self bannerLog: 'Finished ----- ', self basename.
	^ self noResult
%
